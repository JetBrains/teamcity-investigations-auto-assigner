plugins {
  id 'com.github.rodm.teamcity-server' version '1.1.1'
  id 'org.jetbrains.kotlin.jvm' version '1.3.10'
}

apply plugin: 'java'
apply plugin: 'com.github.rodm.teamcity-server'
apply plugin: 'idea'

ext {
  teamcityVersion = project.hasProperty('teamcityVersion') ? property('teamcityVersion') : '2018.2'
  versionNumber = project.hasProperty('versionNumber') ? property('versionNumber') : 'SNAPSHOT-' + new Date().format('yyyyMMddHHmmss')
}

group = 'jetbrains.buildServer.investigationsAutoAssigner'

version = versionNumber

// Remove repositories added by plugins
project.plugins.withType(JavaPlugin) {
  project.repositories.clear()
}

repositories {
  maven { url "https://download.jetbrains.com/teamcity-repository" }
  mavenCentral()
  mavenLocal()
}

configurations {
}

dependencies {
  provided(group: 'org.jetbrains.teamcity.internal', name: 'server', version: "${teamcityVersion}")
  testCompile 'org.mockito:mockito-core:2.18.0'
  testCompile 'org.assertj:assertj-core:2.2.0'
  testCompile 'com.google.jimfs:jimfs:1.1'
  testCompile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

test {
  useTestNG()
}

teamcity {
  version = teamcityVersion

  server {
    descriptor {
      name = project.name
      displayName = 'Investigations Auto-Assigner'
      version = project.version
      vendorName = 'JetBrains, s.r.o.'
      vendorUrl = 'http://www.jetbrains.com/'
      description = 'Executes auto assigning investigations according to a set of rules.'

      useSeparateClassloader = true
      files {
        into('kotlin-dsl') {
          from("${rootProject.projectDir}/kotlin-dsl")
        }
      }
    }
  }

}

// Do not include version into plugin archive name
serverPlugin.version = null

idea {
  module {
    downloadJavadoc = false
    downloadSources = true
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.6'
}

compileKotlin {
  kotlinOptions {
    jvmTarget = "1.8"
  }
}
compileTestKotlin {
  kotlinOptions {
    jvmTarget = "1.8"
  }
}